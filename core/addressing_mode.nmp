//addressing mode of mos6502

//ABSOLUTE MODE
macro ABS(address) = 
    var lo_address[card(8)]= 0;
    var hi_address[card(8)]= 0;

    load(PC, lo_address);
    PC = PC + 1;
    load(PC, hi_address);
    PC = PC + 1;
   address = ((hi_address << 8) | lo_address);


//ABSOLUTE, X MODE
macro ABS_X(address) = 
    var _address[card(16)] = 0;
    ABS(_address);

    if (((_address & 0xFF) + X) & 0x100) then
        cycle = cycle + 1;
    endif;
    address = _address + X;


//ABSOLUTE, Y MODE
macro ABS_Y(address) =
    var _address[card(16)] = 0;
    ABS(_address);

    if (((_address & 0xFF) + Y) & 0x100) then
        cycle = cycle + 1;
    endif;
    address = _address + Y;


//IMMEDIATE VALUE MODE
macro IMMEDIATE(value) =
    load(PC, value);
    PC = PC + 1;


//INDIRECT MODE
macro IND(address) = 
    var _address[card(16)] = 0;
    ABS(_address);
    var lo_address[card(8)]= 0;
    var hi_address[card(8)]= 0;

    load(_address, lo_address);
    load((_address + 1), hi_address);

    address = ((hi_address << 8) | lo_address);


//RELATIVE MODE
macro REL(address) =
    var rel[card(8)]= 0;
    load(PC, rel);
    PC = PC + 1;

    var _address[card(16)] = 0x0000 | rel;

    if (rel & 0x80) then
        _address = 0xFF00 | rel;
    endif;

    address = (PC + _address);


//ZEROPAGE MODE
macro ZPG(address) =
    var _address[card(16)] = 0;
    load(PC, _address);
    PC = PC + 1;
    address = (_address & 0x00FF);


//ZEROPAGE X MODE
macro X_ZPG(address) =
    var _address[card(16)] = 0;
    load(PC, _address);
    PC = PC + 1;

    address = (_address + X) & 0x00FF;


//ZEROPAGE Y MODE
macro Y_ZPG() = 
    var _address[card(16)] = 0;
    load(PC, _address);
    PC = PC + 1;

    address = (_address + Y) & 0x00FF;

    
//PRE INDEXED INDIRECT MODE
macro PRE_IND(address) =
    var _address[card(16)] = 0;
    X_ZPG(_address);

    var lo_address[card(8)]= 0;
    var hi_address[card(8)]= 0;

    load(_address, lo_address);
    load((_address + 1), hi_address);

    address = ((hi_address << 8) | lo_address);

//POST INDEXED INDIRECT MODE
macro POST_IND(address) =
    var _address[card(16)] = 0;
    ZPG(_address);
    
    var lo_address[card(8)]= 0;
    var hi_address[card(8)]= 0;

    load(_address, lo_address);
    load((_address + 1), hi_address);

    var final_address[card(16)] = ((hi_address << 8) | lo_address);

    if (((final_address & 0xFF) + Y) & 0x100) then
        cycle = cycle + 1;
    endif;

    address = (final_address + Y);

